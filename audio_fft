#include <Arduino.h>
#include <arduinoFFT.h>
#include <driver/i2s.h>

#define SAMPLE_BUFFER_SIZE 512
#define SAMPLE_RATE 16000
#define I2S_MIC_CHANNEL I2S_CHANNEL_FMT_ONLY_LEFT
#define I2S_MIC_SERIAL_CLOCK GPIO_NUM_32
#define I2S_MIC_LEFT_RIGHT_CLOCK 25
#define I2S_MIC_SERIAL_DATA GPIO_NUM_33

#define SAMPLES 512
double vReal[SAMPLES];
double vImag[SAMPLES];

i2s_config_t i2s_config = {
    .mode = (i2s_mode_t)(I2S_MODE_MASTER | I2S_MODE_RX),
    .sample_rate = SAMPLE_RATE,
    .bits_per_sample = I2S_BITS_PER_SAMPLE_32BIT,
    .channel_format = I2S_CHANNEL_FMT_ONLY_LEFT,
    .communication_format = I2S_COMM_FORMAT_I2S,
    .intr_alloc_flags = ESP_INTR_FLAG_LEVEL1,
    .dma_buf_count = 4,
    .dma_buf_len = 1024,
    .use_apll = false,
    .tx_desc_auto_clear = false,
    .fixed_mclk = 0};

i2s_pin_config_t i2s_mic_pins = {
    .bck_io_num = I2S_MIC_SERIAL_CLOCK,
    .ws_io_num = I2S_MIC_LEFT_RIGHT_CLOCK,
    .data_out_num = I2S_PIN_NO_CHANGE,
    .data_in_num = I2S_MIC_SERIAL_DATA};

int32_t raw_samples[SAMPLE_BUFFER_SIZE];
ArduinoFFT<double> FFT;

void setup() {
  Serial.begin(115200);
  i2s_driver_install(I2S_NUM_0, &i2s_config, 0, NULL);
  i2s_set_pin(I2S_NUM_0, &i2s_mic_pins);
  delay(1000);
  Serial.println("FFT Ready");
}

void loop()
{
  // read from the I2S device
  size_t bytes_read = 0;
  i2s_read(I2S_NUM_0, raw_samples, sizeof(int32_t) * SAMPLE_BUFFER_SIZE, &bytes_read, portMAX_DELAY);
  int samples_read = bytes_read / sizeof(int32_t);
  for (int i = 0; i < samples_read; i++){
    Serial.printf("%ld\n", raw_samples[i]);
  }

  // Normalize to -1.0 to 1.0
    for (int i = 0; i < samples_read; i++) {
        vReal[i] = (double)raw_samples[i] / (double)INT32_MAX;
        vImag[i] = 0.0;
    } 

  // Remove DC offset  
  double mean = 0;
  for(int i = 0; i < samples_read; i++) {
    mean += vReal[i];
  }
  mean /= samples_read;
  for (int i = 0; i < samples_read; i++){
    vReal[i] -= mean;
  }

  // Perform FFT 
  FFT.windowing(vReal, samples_read, FFT_WIN_TYP_HAMMING, FFT_FORWARD);
  FFT.compute(vReal, vImag, samples_read, FFT_FORWARD);
  FFT.complexToMagnitude(vReal, vImag, samples_read);

  // Print frequency bins and magnitudes
  Serial.println("Frequency (Hz), Magnitude");
    for (int i = 0; i < samples_read / 2; i++) {
      double frequency = (i * SAMPLE_RATE) / samples_read;
      Serial.print(frequency, 2);
      Serial.print(", ");
      Serial.println(vReal[i], 2);
  } 

  // Find peak frequencies
  double peak = 0;
  int peakIndex = 0;

  for (int i = 1; i < samples_read / 2; i++) {
    if (vReal[i] > peak) {
      peak = vReal[i];
      peakIndex = i;
      }
  }

  double freqResolution = (double)SAMPLE_RATE / samples_read;
  double peakFrequency = peakIndex * freqResolution;

  // Serial Monitor 
  Serial.print("Peak Frequency = ");
  Serial.println(peakFrequency, 1);
  Serial.print(" Hz  |  Magnitude: ");
  Serial.println(peak, 4);

  // Serial Plotter 
  for (int i = 1; i < samples_read / 2; i++) {
      Serial.println(vReal[i], 4);
  }
}
